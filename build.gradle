buildscript {
    repositories {
        jcenter()
        mavenCentral()

        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath 'org.akhikhl.gretty:gretty:1.2.4'
        classpath 'com.moowork.gradle:gradle-node-plugin:1.1.1'
    }
}

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'eclipse-wtp'
apply plugin: 'org.akhikhl.gretty'
apply plugin: 'com.moowork.node'
apply plugin: 'com.moowork.gulp'

// JDK 8
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    // Logger
    compile 'ch.qos.logback:logback-classic:1.1.3'

    // Servlet
    compile 'javax.servlet:jstl:1.2'

    // Spring dependencies
    compile 'org.springframework:spring-webmvc:4.1.6.RELEASE'
    compile 'org.springframework.security:spring-security-web:4.2.2.RELEASE'
    compile 'org.springframework.session:spring-session:1.3.0.RELEASE'
}

gretty {
    contextPath = 'gestion-gl'
    httpPort = 8080
    servletContainer = 'tomcat8'
}

// runs the gulp build task on gradle build
build.dependsOn npm_install
build.dependsOn gulp_build

// Run gulp watch when launching app
project.afterEvaluate {
    tasks.appRun.dependsOn npm_install
    tasks.appRun.dependsOn gulp_build
}

// For Eclipse IDE only
eclipse {
    wtp {
        component {
            //define context path, default to project folder name
            contextPath = 'gestion-gl'
        }
    }
}
